#!/usr/bin/env perl

use strict;
use Term::ReadKey;
use Getopt::Long;
use Pod::Usage;
use constant VERSION_STRING => "1.10";

my $debug = 0;
my $flag_help = 0;
my $flag_man = 0;
my $param_memory = 4;
my $param_slots = 1;
GetOptions(
	"m=i"   => \$param_memory,
	"s=i"   => \$param_slots,
	"debug" => \$debug,
	"help"  => \$flag_help,
	"man"   => \$flag_man
) or pod2usage(2);
pod2usage(1) if $flag_help;
pod2usage(-verbose => 2) if $flag_man;

my @lines = `qstat -F -xml`;

my $current_host = undef;
my $current_queue_name = undef;
my $current_queue_type = undef;
my $current_queue_state = undef;
my $current_slots_used;
my $current_slots_resv;
my $current_slots_total;
my $current_mem_req;

my $qname_to_obj = {};
for(@lines) {
	chomp;
	if(m|<Queue-List>|) {
		$current_host = undef;
		$current_queue_name = undef;
		$current_queue_type = undef;
		$current_queue_state = undef;
		$current_mem_req = undef;
		$current_slots_used = 0;
		$current_slots_resv = 0;
		$current_slots_total = 0;
	} elsif(m|<name>(.*?)\@(.*?)</name>|) {
		$current_host = $2;
		$current_queue_name = $1;
	} elsif(m|</Queue-List>|) {
		$qname_to_obj->{$current_queue_name}->{"type"} = $current_queue_type;
		if($current_queue_state =~ m|u|i) {
			$qname_to_obj->{$current_queue_name}->{"slots_error"} += $current_slots_total;
			$qname_to_obj->{$current_queue_name}->{"okjobs"} += 0;
		} else {
			$qname_to_obj->{$current_queue_name}->{"slots_used"} += $current_slots_used;
			$qname_to_obj->{$current_queue_name}->{"slots_resv"} += $current_slots_resv;
			$qname_to_obj->{$current_queue_name}->{"slots_total"} += $current_slots_total;
			my $available_slots = int(($current_slots_total - $current_slots_used - $current_slots_resv) / $param_slots);
			my $njobs = int($current_mem_req / $param_memory);
			print "DR: $njobs / $available_slots\n" if($debug);
			if($njobs > $available_slots) { $njobs = $available_slots; }
			$qname_to_obj->{$current_queue_name}->{"okjobs"} += $njobs;
		}
		$current_host = undef;
		$current_queue_name = undef;
		$current_queue_type = undef;
		$current_queue_state = undef;
		$current_mem_req = undef;
		$current_slots_used = 0;
		$current_slots_resv = 0;
		$current_slots_total = 0;
	} elsif(m|<slots_used>(\d+)</slots_used>|) {
		$current_slots_used = $1;
	} elsif(m|<slots_resv>(\d+)</slots_resv>|) {
		$current_slots_resv = $1;
	} elsif(m|<slots_total>(\d+)</slots_total>|) {
		$current_slots_total = $1;
	} elsif(m|<qtype>(.*?)</qtype>|) {
		$current_queue_type = $1;
	} elsif(m|<state>(.*?)</state>|) {
		$current_queue_state = $1;
	} elsif(m|<resource\s+name="mem_req"\s+type=".*?">([\d\.]+)(\w*)</resource>|) {
		$current_mem_req = $1;
		my $sfx = $2;
		$current_mem_req /= 1000.0 if($sfx =~ m|m|i);
		$current_mem_req /= 1000000.0 if($sfx =~ m|k|i);
	}
}

print "QNAME             USED/SLOTS(ERROR) \%USED  #ready\n";
print "--------------------------------------------------\n";
for my $qname(sort keys %$qname_to_obj) {
	my $obj = $qname_to_obj->{$qname};
	next if($obj->{"type"} eq 'I');
	print sprintf("%-16s %5d/%5d(%5d) %5.1f%%  %5d\n", $qname, $obj->{"slots_used"}, $obj->{"slots_total"}, $obj->{"slots_error"}, $obj->{"slots_used"} * 100.0 / $obj->{"slots_total"}, $obj->{"okjobs"});
}

=pod

=head1 NAME

qfree - Thin Grid Engine wrapper : show the free slots of queues

=head1 SYNOPSIS

qfree [-m dd] [-s dd]

=head1 OPTIONS

=over 8

=item<-m dd>

Set the amount of memory for new jobs.
Please note that this amount is not multiplied by the number of slots given by B<-s> option; although the memory requirement in Grid Engine will be multiplied by the number of used slots.

=item<-s dd>

Set the number of slots for new jobs.

=back

=head1 DESCRIPTION

B<qfree> outputs the number of free slots in batch (non-interactive) queues.
It also outputs the number of jobs that can be immediately executed for each queue.
The number of slots and the memory requirement for each job is 1 and 4GB, respectively, by default.

=cut
